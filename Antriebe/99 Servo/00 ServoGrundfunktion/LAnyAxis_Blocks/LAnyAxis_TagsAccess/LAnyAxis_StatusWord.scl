FUNCTION "LAnyAxis_StatusWord" : Word
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'tech.team.motioncontrol@siemens.com'
FAMILY : AnyAxis
VERSION : 1.0
   VAR_INPUT 
      axis : DB_ANY;
   END_VAR

   VAR_OUTPUT 
      statusWord : DWord;
   END_VAR

   VAR CONSTANT 
      ERROR_INVALID_TECHNOLOGY_OBJECT : Word := 16#8002;
      FINISHED : Word := 16#0000;
   END_VAR


BEGIN
	REGION HEADER
	    //================================================================================
	    // SIEMENS AG
	    // (c)Copyright 2020 All Rights Reserved
	    //--------------------------------------------------------------------------------
	    // Library: LAnyAxis 
	    // Tested with: S7-1515-2 PN
	    // Engineering: TIA Portal V16
	    // Restrictions: -
	    // Requirements: S7-1500 FW 2.8.1
	    // Functionality: Conversion DB_ANY --> TO_Axis
	    // Support: tech.team.motioncontrol@siemens.com
	    // Note: Parameter comments are only available in language 'English (United States)'
	    //--------------------------------------------------------------------------------
	    // Change log table:
	    // Version  Date        Expert in charge  Changes applied
	    // 01.00.00 10.03.2020  RS                First released version
	    //================================================================================
	END_REGION
	
	// Read axis parameter
	CASE TypeOfDB(#axis) OF
	    TO_POSITIONINGAXIS:
	        #statusWord := "LAnyAxis_UnpubStatusWordPosAxis"(#axis);
	        #LAnyAxis_StatusWord := #FINISHED;
	        
	    TO_SYNCHRONOUSAXIS:
	        #statusWord := "LAnyAxis_UnpubStatusWordSyncAxis"(#axis);
	        #LAnyAxis_StatusWord := #FINISHED;
	        
	    TO_EXTERNALENCODER:
	        #statusWord := "LAnyAxis_UnpubStatusWordExtEncoder"(#axis);
	        #LAnyAxis_StatusWord := #FINISHED;
	        
	    TO_SPEEDAXIS:
	        #statusWord := "LAnyAxis_UnpubStatusWordSpeedAxis"(#axis);
	        #LAnyAxis_StatusWord := #FINISHED;
	        
	    ELSE
	        #LAnyAxis_StatusWord := #ERROR_INVALID_TECHNOLOGY_OBJECT; // Invalid TO specification
	END_CASE;
END_FUNCTION

