FUNCTION "ReadActualData" : Word
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'tech.team.motioncontrol@siemens.com'
FAMILY : AnyAxis
VERSION : 1.0
   VAR_INPUT 
      axis : DB_ANY;
   END_VAR

   VAR_OUTPUT 
      ActualData : "UDT_ServoActualData";
   END_VAR

   VAR CONSTANT 
      ERROR_INVALID_TECHNOLOGY_OBJECT : Word := 16#8002;
      FINISHED : Word := 16#0000;
   END_VAR


BEGIN
	// Read axis parameter
	CASE TypeOfDB(#axis) OF
	    TO_POSITIONINGAXIS:
	        #ActualData := "FC_ReadActualData_PosAxis"(#axis);
	        #ReadActualData := #FINISHED;
	        
	    TO_SYNCHRONOUSAXIS:
	        #ActualData := "FC_ReadActualData_SyncAxis"(#axis);
	        #ReadActualData := #FINISHED;
	        
	    TO_SPEEDAXIS:
	        #ActualData := "FC_ReadActualData_SpeedAxis"(#axis);
	        #ReadActualData := #FINISHED;
	        
	    ELSE
	        #ReadActualData := #ERROR_INVALID_TECHNOLOGY_OBJECT; // Invalid TO specification
	END_CASE;
END_FUNCTION

