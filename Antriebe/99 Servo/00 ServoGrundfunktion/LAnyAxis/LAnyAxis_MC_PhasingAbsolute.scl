FUNCTION_BLOCK "LAnyAxis_MC_PhasingAbsolute"
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'tech.team.motioncontrol@siemens.com'
FAMILY : AnyAxis
VERSION : 1.0
   VAR_INPUT 
      Master : DB_ANY;
      Slave : DB_ANY;
      Execute : Bool;
      PhaseShift : LReal;
      Velocity : LReal := -1.0;
      Acceleration : LReal := -1.0;
      Deceleration : LReal := -1.0;
      Jerk : LReal := -1.0;
   END_VAR

   VAR_OUTPUT 
      Done { ExternalWritable := 'False'} : Bool;
      Busy { ExternalWritable := 'False'} : Bool;
      CommandAborted { ExternalWritable := 'False'} : Bool;
      Error { ExternalWritable := 'False'} : Bool;
      ErrorId { ExternalWritable := 'False'} : Word;
   END_VAR

   VAR 
      instMC {InstructionName := 'MC_PHASINGABSOLUTE'; LibVersion := '6.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : MC_PHASINGABSOLUTE;
      statExecuteOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      statAxis { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DB_ANY;
      statAxisType { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;
      statMaster { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DB_ANY;
      statMasterType { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;
   END_VAR

   VAR CONSTANT 
      ERROR_INVALID_TECHNOLOGY_OBJECT : Word := 16#8002;
      IDLE : Int := 0;
      INVALID_AXIS : Int := -1;
      INVALID_MASTER : Int := -2;
      EXTERNAL_ENCODER : Int := 1;
      POSITIONING_AXIS : Int := 3;
      SYNCHRONOUS_AXIS : Int := 4;
      PROXY : Int := 5;
   END_VAR


BEGIN
	REGION HEADER
	    //================================================================================
	    // SIEMENS AG
	    // (c)Copyright 2020 All Rights Reserved
	    //--------------------------------------------------------------------------------
	    // Library: LAnyAxis 
	    // Tested with: S7-1515T-2 PN
	    // Engineering: TIA Portal V16
	    // Restrictions: -
	    // Requirements: S7-1500T FW 2.8.1
	    // Functionality: Conversion DB_ANY --> TO_Axis
	    // Support: tech.team.motioncontrol@siemens.com
	    // Note: Parameter comments are only available in language 'English (United States)'
	    //--------------------------------------------------------------------------------
	    // Change log table:
	    // Version  Date        Expert in charge  Changes applied
	    // 01.00.00 10.03.2020  RS                First released version
	    //================================================================================
	END_REGION
	
	REGION EXECUTE
	    IF #Execute AND NOT #statExecuteOld THEN
	        // Copy parameter
	        #instMC.PhaseShift := #PhaseShift;
	        #instMC.Velocity := #Velocity;
	        #instMC.Acceleration := #Acceleration;
	        #instMC.Deceleration := #Deceleration;
	        #instMC.Jerk := #Jerk;
	        
	        // Check axis type
	        CASE TypeOfDB(#Slave) OF
	            TO_SYNCHRONOUSAXIS:
	                #statAxisType := #SYNCHRONOUS_AXIS;
	                #statAxis := #Slave;
	                
	                // Check master type
	                CASE TypeOfDB(#Master) OF
	                    TO_POSITIONINGAXIS:
	                        #statMasterType := #POSITIONING_AXIS;
	                        #statMaster := #Master;
	                        
	                    TO_SYNCHRONOUSAXIS:
	                        #statMasterType := #SYNCHRONOUS_AXIS;
	                        #statMaster := #Master;
	                        
	                    TO_EXTERNALENCODER:
	                        #statMasterType := #EXTERNAL_ENCODER;
	                        #statMaster := #Master;
	                        
	                    ELSE
	                        #statMasterType := #INVALID_AXIS;
	                        #statMaster := 0;
	                        
	                        // Invalid master
	                        #statAxisType := #INVALID_MASTER;
	                END_CASE;
	                
	            ELSE
	                #statAxisType := #INVALID_AXIS;
	                #statAxis := 0;
	        END_CASE;
	        
	    ELSIF NOT #Execute AND (#statAxisType < #IDLE) THEN
	        #statAxisType := #IDLE;
	        
	        // Outputs
	        #Done := FALSE;
	        #Busy := FALSE;
	        #CommandAborted := FALSE;
	        #Error := FALSE;
	        #ErrorId := 16#0000;
	    END_IF;
	    
	    // Edge detection
	    #statExecuteOld := #Execute;
	END_REGION
	
	CASE #statAxisType OF
	    #IDLE:
	        ;
	        
	    #SYNCHRONOUS_AXIS:
	        REGION VALID_AXIS
	            // Inputs  
	            #instMC.Execute := #Execute;
	            
	            // Call of MC instruction
	            CASE #statMasterType OF
	                #POSITIONING_AXIS:
	                    "LAnyAxis_UnpubPhasingAbsoluteMasterPosAxis"(slave := #statAxis,
	                                                                 master := #statMaster,
	                                                                 instMC := #instMC);
	                #SYNCHRONOUS_AXIS:
	                    "LAnyAxis_UnpubPhasingAbsoluteMasterSyncAxis"(slave := #statAxis,
	                                                                  master := #statMaster,
	                                                                  instMC := #instMC);
	                #EXTERNAL_ENCODER:
	                    "LAnyAxis_UnpubPhasingAbsoluteMasterExtEncoder"(slave := #statAxis,
	                                                                    master := #statMaster,
	                                                                    instMC := #instMC);
	                    
	            END_CASE;
	            
	            // Outputs
	            #Done := #instMC.Done;
	            #Busy := #instMC.Busy;
	            #CommandAborted := #instMC.CommandAborted;
	            #Error := #instMC.Error;
	            #ErrorId := #instMC.ErrorId;
	            
	            // Status of MC instruction
	            IF NOT #instMC.Done
	                AND NOT #instMC.Busy
	                AND NOT #instMC.CommandAborted
	                AND NOT #instMC.Error
	            THEN
	                // Not active
	                #statAxisType := #IDLE;
	            END_IF;
	        END_REGION
	        
	    ELSE
	        REGION INVALID_TECHNOLOGY_OBJECT
	            // Outputs
	            #Done := FALSE;
	            #Busy := FALSE;
	            #CommandAborted := FALSE;
	            #Error := TRUE;
	            #ErrorId := #ERROR_INVALID_TECHNOLOGY_OBJECT;
	        END_REGION
	END_CASE;
END_FUNCTION_BLOCK

